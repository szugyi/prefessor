buildscript {
    ext.kotlin_version = '1.2.41'
    ext.android_gradle_build_tools_version = '3.1.0'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_build_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group 'digital.wup.prefessor'
    version '0.1.0-SNAPSHOT'

    repositories {

        google()
        jcenter()
    }
}

configure(subprojects) {

    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.23.1'
    }

    task ktlint(type: JavaExec, group: LifecycleBasePlugin.VERIFICATION_GROUP) {
        inputs.dir('src')
        outputs.dir('src')

        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = 'com.github.shyiko.ktlint.Main'
        args '--android', 'src/**/*.kt'
    }

    task ktFormat(type: JavaExec, group: 'formatting') {
        inputs.dir('src')
        outputs.dir('src')

        description = 'Fix Kotlin code style deviations.'
        classpath = configurations.ktlint
        main = 'com.github.shyiko.ktlint.Main'
        args '--android', '-F', 'src/**/*.kt'
    }

    afterEvaluate {
        check.dependsOn ktlint
    }
}
